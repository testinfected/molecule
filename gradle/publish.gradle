apply plugin: 'com.bmuschko.nexus'

def optionalDeps = []
ext {
  optional = { optionalDeps << it; it }
}

modifyPom {
  project {
    name 'Molecule'
    description 'A web micro-framework for Java'
    url 'http://vtence.com/molecule'
    packaging 'jar'
    licenses {
      license {
        name 'MIT License'
        url 'http://www.opensource.org/licenses/mit-license.php'
        distribution 'repo'
      }
    }
    scm {
      url 'https://github.com/testinfected/molecule'
      connection 'scm:git:https://github.com/testinfected/molecule.git'
      developerConnection 'scm:git@github.com:testinfected/molecule.git'
    }
    developers {
      developer {
        id 'testinfected'
        name 'Vincent TencÃ©'
      }
    }

    optionalDeps.each { dep ->
      dependencies.find { it.artifactId == dep.name }.optional = true
    }

    // no need to publish test dependencies
    dependencies.removeAll { it.scope == "test" }
  }
}